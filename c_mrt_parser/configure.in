dnl Process this file with autoconf to produce a configure script.
AC_REVISION($Revision$)

AC_INIT([libbgpgill], 1.0, [contact@bgproutes.io])
AC_CONFIG_SRCDIR([file_buffer.c])
AC_CONFIG_HEADERS([gillstream-config.h])

CFLAGS="-g $CFLAGS -Wall"
CFLAGS="$CFLAGS -Wsystem-headers -Wno-format-y2k -Wno-sign-compare -Wcast-align"
CFLAGS="$CFLAGS -Wmissing-prototypes -Wpointer-arith -Wreturn-type -Wswitch -Wshadow"

case `uname` in
	Darwin*) CFLAGS="$CFLAGS -Wno-nullability-completeness" ;;
esac

# Checks for programs.
AC_PROG_CC
AC_PROG_RANLIB

# Checks for header files.
AC_CHECK_HEADERS([arpa/inet.h netinet/in.h])
AC_STRUCT_TM

# Check for u_*_t
AC_CHECK_TYPE(u_char_t, , AC_DEFINE(u_char_t, uchar_t, [Define if system headers do not define u_char_t]))
AC_CHECK_TYPE(u_int8_t, , AC_DEFINE(u_int8_t, uint8_t, [Define if system headers do not define u_int8_t]))
AC_CHECK_TYPE(u_int16_t, , AC_DEFINE(u_int16_t, uint16_t, [Define if system headers do not define u_int16_t]))
AC_CHECK_TYPE(u_int32_t, , AC_DEFINE(u_int32_t, uint32_t, [Define if system headers do not define u_int32_t]))

AC_CHECK_LIB(z, gzopen, [], AC_MSG_ERROR([libz not found],1))
AC_CHECK_LIB(bz2, BZ2_bzReadOpen, [], AC_MSG_ERROR([libbzip2 not found],1))


# Check for inet_ntoa in -lnsl if not found (Solaris)
AC_CHECK_FUNCS(inet_ntoa, [], AC_CHECK_LIB(nsl, inet_ntoa, [], AC_MSG_ERROR([inet_ntoa not found],1)))
AC_CHECK_FUNCS(inet_ntop, [], AC_CHECK_LIB(nsl, inet_ntop, [], AC_MSG_ERROR([inet_ntop not found],1)))

case `uname` in
	Darwin*) SOFLAGS="$SOFLAGS -dynamiclib" ;;
	*)       SOFLAGS="$SOFLAGS -shared" ;;
esac

AC_ARG_WITH([libdir],
    [AS_HELP_STRING([--with-libdir=DIR], [Specify the library installation directory (default is ${prefix}/lib)])],
    [libdir="$withval"],
    [libdir="\${prefix}/lib"]
)

AC_SUBST(libdir)
AC_SUBST(CFLAGS)
AC_SUBST(LIBS)
AC_SUBST(SOFLAGS)

AC_CONFIG_FILES([Makefile])
AC_OUTPUT